plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

repositories {
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
    }
    maven {
        name "Shedaniel"
        url "https://maven.shedaniel.me/"
    }
    maven {
        name "Modrinth"
        url "https://api.modrinth.com/maven"
    }
    maven {
        name "Jared"
        url "https://maven.blamejared.com"
    }
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    
    // ClothConfig API for configuration GUI
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}"
    
    // Applied Energistics 2
    modImplementation "appeng:appliedenergistics2-forge:15.3.3"

    // Development-only dependencies (Jade and JEI)
    modLocalRuntime "maven.modrinth:jade:11.11.1+forge"
    modLocalRuntime "mezz.jei:jei-1.20.1-forge:15.20.0.105"

    implementation(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    developmentForge(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand(
            "mod_id": rootProject.mod_id,
            "mod_version": rootProject.mod_version,
            "mod_display_name": rootProject.mod_display_name,
            "mod_authors": rootProject.mod_authors,
            "mod_description": rootProject.mod_description,
            "mod_license": rootProject.mod_license,
            "version": project.version
        )
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("forge")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
